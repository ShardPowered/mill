plugins {
    id 'java'
    id 'maven-publish'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'me.tassu'
version '1.1.0'
defaultTasks 'licenseFormat', 'build'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://maven.tassu.me/'
        name 'tassu-repo'
    }
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/maven'
    }
    maven {
        name 'paper-repo'
        url 'https://papermc.io/repo/repository/maven-public'
    }
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name 'aikar-repo'
        url "https://ci.emc.gs/nexus/content/groups/aikar/"
    }
}

dependencies {
    compileOnly "me.tassu:easy:1.1.3"
    compileOnly 'me.tassu:snake:1.1.0'
    compileOnly 'com.destroystokyo.paper:paper-api:1.13.2-R0.1-SNAPSHOT'

    testCompile "me.tassu:easy:1.1.3"
    testCompile 'me.tassu:snake:1.1.0'
    testCompile 'com.destroystokyo.paper:paper-api:1.13.2-R0.1-SNAPSHOT'
    testCompile 'junit:junit:4.12'
}

jar {
    from 'LICENSE'
}

license {
    header = project.file('LICENSE')
    include '**/*.java'
    newLine = true
}

build.dependsOn(shadowJar)

shadowJar {
    classifier = null

    relocate 'me.tassu.util', 'me.tassu.easy.util'
    relocate 'ninja.leaping.configurate', 'me.tassu.easy.lib.configurate'
    relocate 'com.google.inject', 'me.tassu.easy.google.inject'
    relocate 'com.typesafe.config', 'me.tassu.easy.lib.typesafe'
    relocate 'co.aikar', 'me.tassu.easy.lib.aikar'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'mill'

            artifact sourcesJar
            artifact javadocJar

            from components.java
        }
    }

    repositories {
        maven { url "file:///home/tassu/maven" }
    }
}
